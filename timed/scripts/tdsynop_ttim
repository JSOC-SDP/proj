#!/bin/csh -f
# script to run a single instance of trackings plus analyses in the 
#   time-distance synoptic series for a given time center
# Prototype!
# This script should be run every 8 hours

set TW
@ TEST = 0	# set to 1 for production in namespace hmi_test
if ("$1" =~ '-test') then
  @ TEST = 1
  set TW = test
  shift
endif

if ($#argv < 2) then
  echo "usage: $0 YYMMDD HR"
  exit
endif
@ YYMMDD = `echo $1 | sed  -e "s/^0*//"`
@ HR = `echo $2 | sed  -e "s/^0*//"`

@ SKIPTRK = 0
@ SKIPFIT = 0
set PROG = `basename $0`
if ($PROG !~ '*tdsynop*') then
  if ($?JOB_NAME) then
    set PROG = $JOB_NAME
  endif
endif
if ($PROG =~ "*_ttim") @ SKIPTRK = 1
if ($PROG =~ "*_inv") @ SKIPFIT = 1

# modules and programs
set GENTG = gentargs
set TRACK = mtrack
set TTFIT = travel_times
set TDINV = invert_td_hr

if ($TEST) then
  set TSTOPT = ~rick/hmi/timed/testoptions
  if (!(-r $TSTOPT)) then
    echo Error: unable to read file $TSTOPT for test options
    exit
  endif
  set NMSP = hmi_test
  grep -q "^GENTG =" $TSTOPT
  if ($status == 0) set GENTG = `grep "^GENTG =" $TSTOPT | awk '{print $3}'`
  grep -q "^TRACK =" $TSTOPT
  if ($status == 0) set TRACK = `grep "^TRACK =" $TSTOPT | awk '{print $3}'`
  grep -q "^TTFIT =" $TSTOPT
  if ($status == 0) set TTFIT = `grep "^TTFIT =" $TSTOPT | awk '{print $3}'`
  grep -q "^TDINV =" $TSTOPT
  if ($status == 0) set TDINV = `grep "^TDINV =" $TSTOPT | awk '{print $3}'`
else
  set NMSP = hmi
endif

#  check that required scripts and programs are in path or rooted
foreach CMD ($GENTG $TRACK $TTFIT)
  which $CMD > /dev/null
  if ($status) then
    if (!(-x $CMD)) then
      echo Error: command $CMD not in path\; script will fail
      exit
    endif
  endif
end

# add flags
set GENTG = "$GENTG grid= timed24+"
set TRACK = "$TRACK -vo"
set TTFIT = "$TTFIT -v"
set TDINV = "$TDINV -v"

# base naming convention for I/O series and logs
set NMSP = hmi
set BASE = tdV
set SUFX = _synop
set PLID = hmitdV
set TYPE = $NMSP.$BASE$SUFX

# parameters
@ LNGTH = 640
@ SIZE = 512
set SCALE = 0.06
set QMASK = 0x8004000
set MMISS = 5000

# output series
set TSER = $NMSP.$BASE\track$SUFX
set OSER = $NMSP.$BASE\times$SUFX
set RSER = hmi_test.$BASE\invrt$SUFX

# input series
set ISER = hmi.V_45s
set PSER = hmi.tdpixlist
set BCKG = hmi.V_avg120
set KSER = su_rsb.tdgenkern

if ($TEST) then
  grep -q "^ISER =" $TSTOPT
  if ($status == 0) set ISER = `grep "^ISER =" $TSTOPT | awk '{print $3}'`
  grep -q "^PSER =" $TSTOPT
  if ($status == 0) set MSER = `grep "^PSER =" $TSTOPT | awk '{print $3}'`
  grep -q "^BCKG =" $TSTOPT
  if ($status == 0) set BCKG = `grep "^BCKG =" $TSTOPT | awk '{print $3}'`
  grep -q "^KSER =" $TSTOPT
  if ($status == 0) set GSER = `grep "^KSER =" $TSTOPT | awk '{print $3}'`
  grep -q "^TSER =" $TSTOPT
  if ($status == 0) set GSER = `grep "^TSER =" $TSTOPT | awk '{print $3}'`
  grep -q "^OSER =" $TSTOPT
  if ($status == 0) set GSER = `grep "^OSER =" $TSTOPT | awk '{print $3}'`
  grep -q "^RSER =" $TSTOPT
  if ($status == 0) set GSER = `grep "^RSER =" $TSTOPT | awk '{print $3}'`
endif

#  check that input and product series exist
foreach SER ($ISER $PSER $BCKG $KSER $TSER $OSER $RSER)
  show_info -j ds= $SER >& /dev/null
  if ($status) then
    echo Error: series $SER does not exist\; full script will fail
    exit
  endif
end

set PLOC = $PSER'"[]"'

# ensure target times at uniform intervals
if ($YYMMDD < 500000) @ YYMMDD += 20000000
if ($YYMMDD < 1000000) @ YYMMDD += 19000000
@ YR = $YYMMDD / 10000
@ MO = ($YYMMDD % 10000) / 100
@ DA = $YYMMDD % 100
if ($HR < 8) then
  @ HH = 4
else if ($HR < 16) then
  @ HH = 12
else
  @ HH = 20
endif
set TMID = $YR.`printf %02d $MO`.`printf %02d $DA`_`printf %02d $HH`:00_TAI
@ YY = $YR % 100
set CTIME = `printf %02d $YY``printf %02d $MO``printf %02d $DA`:`printf %02d $HH`

# auxiliary files
set HOSTNAME = `hostname`
set HOSTID = `basename $HOSTNAME .Stanford.EDU`
set PROCID = $HOSTID\_$$
set NOTE = ~rick/rings/notify
set RJCT = ~rick/hmi/qual/reject.V
set TEMP = /tmp23/`whoami`
if (!(-d $TEMP)) set TEMP = /tmp
set TMPB = $TEMP/$PLID$SUFX
if (!(-d $TMPB)) mkdir $TMPB
set TMPD = $TMPB/scripts
if (!(-d $TMPD)) mkdir $TMPD
set MSGS = $TEMP/msgs
if (!(-d $MSGS)) mkdir $MSGS
set SCRT = /scr21/`whoami`
if (!(-d $SCRT)) set SCRT = /tmp
set SCRB = $SCRT/$PLID
if (!(-d $SCRB)) mkdir $SCRB
set LOGF = $SCRB/runlog$SUFX
set RLOG = $TMPB/$CTIME
if (-e $RLOG) mv $RLOG $RLOG.bck
set MMSG = $MSGS/msg.$PROCID
set TLIST = $TMPB/td_inf.$$
set TIMES = $SCRB/latency
set DTIME = $SCRB/synop

set NOW = `date +%Y.%m.%d_%T`
set STARTED = $NOW
onintr INTERRUPT

set TTIME = '*'
set PTIME = '*'
set VTIME = '*'
@ STRTSTOP = 1
if ($STRTSTOP) @ PBEGIN = `date +%s`

# check coverage

# set up logging, notification
set SUBJ = "HMI time-distance $TW processing for $CTIME"
if ($SKIPTRK || $SKIPFIT) then
  echo "$NOW resume $CTIME" > $MMSG
  @ STRTSTOP = 0
else
  echo "$NOW begin $CTIME" > $MMSG
  @ STRTSTOP = 1
endif
cat $MMSG >> $LOGF

$GENTG time= $TMID > $TLIST
set LATS = (`head -1 $TLIST`)
set LONS = (`tail -1 $TLIST`)
@ RGNCT = $#LATS
if ($#LONS > $RGNCT) @ RGNCT = $#LONS

set LATLIST
set LONLIST
@ N = 0
while ($N < $#LATS)
  @ N++
  set LATLIST = "$LATLIST, $LATS[$N]"
end
@ N = 0
while ($N < $#LONS)
  @ N++
  set LONLIST = "$LONLIST, $LONS[$N]"
end
rm $TLIST

if ($SKIPFIT) goto SKIP_TIMES
if ($SKIPTRK) goto SKIP_TRACK

# check for background average
set LINE = (`sunephem $TMID | head -3 | tail -1`)
@ CROT = `echo $LINE[7] | awk -F. '{print $1}'`
@ CLON = `echo $LINE[2] | awk -F. '{print $1}'`
@ CR60 = $CROT
if ($CLON > 330) then
  @ CL60 = 360
else if ($CLON > 270) then
  @ CL60 = 300
else if ($CLON > 210) then
  @ CL60 = 240
else if ($CLON > 150) then
  @ CL60 = 180
else if ($CLON > 90) then
  @ CL60 = 120
else if ($CLON > 30) then
  @ CL60 = 60
else
  @ CR60++
  @ CL60 = 360
endif
set BCK = $BCKG"[$CR60][$CL60]"
@ BCKCT = `show_info -c ds= "$BCK" | awk '{print $1}'`
if ($BCKCT < 1) then
  echo "Warning: Data average record $BCK does not exist"  >> $MMSG
  echo "         Background subtraction skipped"  >> $MMSG
  set BCK = "Not Specified"
endif

@ BEGUN = `date +%s`
$TRACK out= $TSER in= $ISER tmid= $TMID length= $LNGTH \
  bckgn= "$BCK" qmask= $QMASK max_miss= $MMISS reject= $RJCT \
  scale= $SCALE cols= $SIZE rows= $SIZE \
  lat= "{$LATLIST}" lon= "{$LONLIST}" >>& $RLOG
if ($status) then
  set SUBJ = "$SUBJ incomplete"
  echo "  tracking failure on `hostname`"  >> $MMSG
  echo "  run log is on $RLOG"  >> $MMSG
  echo "  tracking failure on `hostname`"  >> $LOGF
  @ STRTSTOP = 0
  goto CLEANUP
else
  @ FINIS = `date +%s`
  @ ELAPS = $FINIS - $BEGUN
  @ ELAPS /= 60
  echo "  time for tracking $RGNCT regions on `hostname`: $ELAPS min"  >> $MMSG
  echo "    tracked data in $TSER""[$TMID]" >> $MMSG
  echo "  ($CTIME) time for tracking $RGNCT regions on `hostname`: $ELAPS min"  >> $LOGF
  set TTIME = $ELAPS
endif

SKIP_TRACK:

set LONSET = ($LONS[1] $LONS[2] $LONS[3] $LONS[4] $LONS[5])
@ BEGUN = `date +%s`
@ RGNCT = `show_info -cq ds= $TSER"[$TMID]"`
set LAT = $LATS[1]
# prepare script for batch processing of travel time fitting
set FILE = td`echo $CTIME | tr ':' '.'`
set SCRIPT = $TMPD/$FILE
echo "#\!/bin/csh -f" > $SCRIPT
# echo "limit vm 3000M" >> $SCRIPT
# if there is a limit it should be at least 3500M
echo 'set LATS =  ('"$LATS"')' >> $SCRIPT
echo 'set LONS =  ('"$LONS"')' >> $SCRIPT
echo 'set LAT = $LATS[$SGE_TASK_ID]' >> $SCRIPT
echo 'set LON = $LONS[$SGE_TASK_ID]' >> $SCRIPT
echo "$TTFIT \" >> $SCRIPT
echo '  in= '$TSER'"['$TMID'][$LON][$LAT]" \' >> $SCRIPT
echo "  pxloc= "'"'$PLOC'" \' >> $SCRIPT
echo "  out= $OSER "'>>&' $RLOG >> $SCRIPT
qsub -sync yes -t 1-$RGNCT -q j.q $SCRIPT >>& $RLOG
@ QJOK = `grep "exited with exit code 0." $RLOG | wc -l`
@ FAIL = $RGNCT - $QJOK
if ($FAIL) then
  @ FDCT = `show_info -cq ds= $OSER"[$TMID]"`
  set SUBJ = "$SUBJ incomplete"
  echo "  $FAIL travel-time failures on cluster" >> $MMSG
  echo "  $FDCT records in $OSER""[$TMID]" >> $MMSG
  echo "  ($CTIME) $FAIL travel-time failures on cluster" >> $LOGF
  echo "  qsub script is $SCRIPT" >> $MMSG
  echo "  qsub script is $SCRIPT" >> $LOGF
  @ STRTSTOP = 0
  goto CLEANUP
else
  @ FINIS = `date +%s`
  @ ELAPS = $FINIS - $BEGUN
  @ ELAPS += 30
  @ ELAPS /= 60
  echo "  time for $RGNCT travel times on the cluster: $ELAPS min"  >> $MMSG
  echo "    travel-time fits in $OSER""[$TMID]" >> $MMSG
  echo "  ($CTIME) time for $RGNCT travel times on the cluster: $ELAPS min"  >> $LOGF
  set PTIME = $ELAPS
endif
if (!($FAIL)) rm $SCRIPT

SKIP_TIMES:

@ BEGUN = `date +%s`
foreach KTYP (Raypath Born)
  set KSET = $KSER"[$KTYP]"
  foreach FTYP (Gabor GizonBirch)
    $TDINV in= $OSER"[$TMID]" kernels= "$KSET" out= $RSER fitting= $FTYP >>& $RLOG
    if ($status) then
      set SUBJ = "$SUBJ incomplete"
      echo "  inversion failure on `hostname`"  >> $MMSG
      echo "  run log is on $RLOG"  >> $MMSG
      echo "  inversion failure on `hostname`"  >> $LOGF
      @ STRTSTOP = 0
      goto CLEANUP
    endif
  end
end
@ RPCT = `show_info -cq ds= $RSER"[$TMID]"`
@ FINIS = `date +%s`
@ ELAPS = $FINIS - $BEGUN
@ ELAPS /= 60
echo "  time for inverting $RPCT regions on `hostname`: $ELAPS min"  >> $MMSG
echo "    inversions in $RSER""[$TMID]" >> $MMSG
echo "  ($CTIME) time for inverting $RPCT regions on `hostname`: $ELAPS min"  >> $LOGF
set VTIME = $ELAPS
goto CLEANUP

INTERRUPT:
set SUBJ = "$SUBJ interrupted"
set TYPE = misc
echo "-> Script "$PROG" or module aborted! <-" >> $MMSG
echo "$CTIME $STARTED  aborted" >> $TIMES
set noglob
echo $CTIME"\t"$TTIME"\t"$PTIME"\t"$VTIME"\t"aborted >> $DTIME
unset noglob
set RGNCT = '?'
@ STRTSTOP = 0

CLEANUP:
set NOW = `date +%Y.%m.%d_%T`
set NOTIFY = `grep ^$TYPE $NOTE | awk '{print $2}'`
if ($STRTSTOP) then
  @ ELAPS = $FINIS - $PBEGIN
  @ ELAPS += 30
  @ ELAPS /= 60
  @ HREL = $ELAPS / 60
  @ MNEL = $ELAPS % 60
else
  @ HREL = 0
  @ MNEL = 0
  echo -n "# " >> $TIMES
endif
echo "$CTIME $STARTED  $HREL`printf :%02d $MNEL`" >> $TIMES
set noglob
echo $CTIME"\t"$TTIME"\t"$PTIME"\t"$VTIME >> $DTIME
unset noglob
echo "$NOW finish $CTIME" >> $MMSG
if ($STRTSTOP) then
  echo "  End-to-end latency = $HREL`printf :%02d $MNEL`" >> $MMSG
endif
echo $SUBJ > $MSGS/subj.$PROCID
if ($#NOTIFY) then
  echo "$NOTIFY" > $MSGS/list.$PROCID
else
  echo rick > $MSGS/list.$PROCID
endif
echo "$NOW finish $CTIME ($RGNCT regions)" >> $LOGF
if (-e $TLIST) rm $TLIST
