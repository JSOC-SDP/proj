#!/bin/csh -f
# script to run multiple instances of trackings plus analyses in the
#   time-distance synoptic series for a set of given time centers
# The invoked scripts should be run every 8 hours

@ TEST = 0	# set to 1 for production in namespace hmi_test
if ("$1" =~ '-test') then
  @ TEST = 1
  shift
endif

if ($#argv < 1) then
  echo "usage: $0 YYMMDD [YYMMDD] [spacing]"
  exit
endif
@ YYMMDD0 = `echo $1 | sed  -e "s/^0*//"`
@ YYMMDD1 = $YYMMDD0

@ SLEEP = 480

if ($#argv > 1) then
  @ YYMMDD1 = `echo $2 | sed  -e "s/^0*//"`
  if ($#argv > 2) @ SLEEP = `echo $3 | sed  -e "s/^0*//"`
endif
@ SLEEP *= 60

if ($TEST) then
  set SCRIPT = ~rick/hmi/timed/scripts/tdsynop
else
  set SCRIPT = tdsynop
endif

foreach CMD ($SCRIPT)
  which $CMD > /dev/null
  if ($status) then
    if (!(-x $CMD)) then
      echo Error: command $CMD not in path\; script will fail
      exit
    endif
  endif
end

@ YYMMDD = $YYMMDD0
set MOLEN = (31 28 31 30 31 30 31 31 30 31 30 31)
@ YR = $YYMMDD / 10000
if ($YR % 4 == 0) set MOLEN = (31 29 31 30 31 30 31 31 30 31 30 31)

while ($YYMMDD <= $YYMMDD1)
  foreach HR (04 12 20)
    qsub -q j.q $SCRIPT $YYMMDD $HR
    if ($YYMMDD < $YYMMDD1) sleep $SLEEP
    if ($YYMMDD == $YYMMDD1 && $HR < 20) sleep $SLEEP
  end
  @ YR = $YYMMDD / 10000
  @ MO = ($YYMMDD % 10000) / 100
  @ DA = $YYMMDD % 100
  @ DA++
  if ($DA > $MOLEN[$MO]) then
    @ MO++
    @ DA = 1
    if ($MO > 12) then
      @ MO = 1
      @ YR++
      set MOLEN = (31 28 31 30 31 30 31 31 30 31 30 31)
      if ($YR % 4 == 0) set MOLEN = (31 29 31 30 31 30 31 31 30 31 30 31)
    endif
  endif
  @ YYMMDD = $YR`printf %02d $MO``printf %02d $DA`
end
